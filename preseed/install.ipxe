#!ipxe

# To access this file via http use following redirection:
# http://preseed.panticz.de/ipxe/menu.ipxe

# choose cpu architecture
cpuid --ext 29 && set arch amd64 || set arch i386

set mainmenu http://192.168.0.2/a.ipxe

:start
# choose boot image
menu Please choose an operating system to boot
item
item --gap URL: ${mainmenu}
item --gap MAC: ${mac}
item --gap CPU: ${arch}
item
item localboot  Boot from first harddisk
item
item --gap Images:
item livecd     + LiveCD...
item install    + NetInstal...
item memtest    Memtest86+
item hdt        Hardware Detection Tool
item live       Tiny Core Linux
item intelflash Intel BootUtil
item debian7	Install Debian7
item debian8	Install Debian8
item dosboot    dosboot
item
item config     iPXE config menu
choose --default exit --timeout 10000 target && goto ${target}

:shell
echo Type 'exit' to get the back to the menu
shell
set menu-timeout 0
set submenu-timeout 0
goto start

:failed
echo Booting failed, dropping to shell

:config
config

:livecd
chain http://preseed.panticz.de/ipxe/menu.livecd.ipxe

:install
chain http://preseed.panticz.de/ipxe/menu.netinstall.ipxe

:localboot
sanboot --no-describe --drive 0x80

:memtest
chain http://www.memtest.org/download/5.01/memtest86+-5.01.bin

:hdt
set 209:string ipxe/hdt/hdt_052.conf
set 210:string http://preseed.panticz.de/
chain ipxe/pxelinux.0
#imgfetch http://www.hdt-project.org/raw-attachment/wiki/hdt-0.5.0/hdt-0.5.2.img.gz
#chain http://preseed.panticz.de/ipxe/memdisk

:intelflash
imgfetch http://preseed.panticz.de/ipxe/intelflash.img
chain http://preseed.panticz.de/ipxe/memdisk

:dosboot
imgfetch http://preseed.panticz.de/ipxe/dosboot.img
chain http://preseed.panticz.de/ipxe/memdisk

:live
#imgfetch http://ftp-stud.hs-esslingen.de/pub/Mirrors/releases.ubuntu.com/12.10/ubuntu-12.10-desktop-amd64.iso
# chain http://preseed.panticz.de/ipxe/memdisk iso
initrd http://distro.ibiblio.org/tinycorelinux/4.x/x86/release/distribution_files/core.gz
chain http://distro.ibiblio.org/tinycorelinux/4.x/x86/release/distribution_files/vmlinuz
boot

:debian7
set release wheezy
set arch amd64
goto debian-install

:debian8
set release jessie
set arch amd64
goto debian-install

:debian-install
prompt Installing Debian ${release} ${arch} via Network
initrd http://192.168.0.2/netboot/${arch}/${release}/initrd.gz
chain http://192.168.0.2/netboot/${arch}/${release}/linux locale=en_US.UTF-8 keymap=us hostname=${release}-${arch}-seed domain=unassigned-domain url=http://192.168.0.2/${release}.seed || goto failed
goto start
